{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "binding-israeli",
   "metadata": {},
   "source": [
    "# Towers of Hanoi\n",
    "\n",
    "This problem is derived from the [introductory manual](http://wp.doc.ic.ac.uk/arusso/wp-content/uploads/sites/47/2015/01/clingo_guide.pdf), but written out so that the code is commented in detail about what each exprssion means. Let's first start with a summary of the problem.\n",
    "\n",
    "## Summary of Task\n",
    "\n",
    "In this puzzle, you have three pegs and discs of different sizes. The discs start on one peg, stacked so that no disk is on top of another disk that is smaller than it. Your goal is to move all the discs from the peg on the left to the peg on the right, with the following constraints:\n",
    "\n",
    "1. You can only move a peg on the top of a stack\n",
    "2. You can only put a peg on top of another one that is larger.\n",
    "\n",
    "If you were doing this with actual pegs, you would probably think about a sequence of moves. But with Answer Set Programming, the cool part is that you can define the pieces of the problem (the pegs, disks) along with the rules, define the ideal solution, and let the solver do its magic. We will proceed to take the same solution provided in the pdf, and annotate it extensively for you to quickly see, understand, and run.\n",
    "\n",
    "## 1. Create Objects\n",
    "\n",
    "Conceptually, let's talk about the objects that we want to model first as facts or atoms. The first are fairly straight forward - we will model the physical:\n",
    "\n",
    "1. pegs\n",
    "2. disks"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 26,
   "id": "basic-ambassador",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "clingo version 5.4.0\n",
      "Reading from stdin\n",
      "Solving...\n",
      "Answer: 1\n",
      "disk(1)\n",
      "disk(2)\n",
      "disk(3)\n",
      "disk(4)\n",
      "peg(a)\n",
      "peg(b)\n",
      "peg(c)\n",
      "SATISFIABLE\n",
      "\n",
      "Models       : 1\n",
      "Calls        : 1\n",
      "Time         : 0.000s (Solving: 0.00s 1st Model: 0.00s Unsat: 0.00s)\n",
      "CPU Time     : 0.000s\n"
     ]
    }
   ],
   "source": [
    "%%clingo --out-ifs=\\\\n 0\n",
    "\n",
    "% Create three pegs, a,b, and c. This is akin to peg(a). peg(b). peg(c).\n",
    "% Putting them together like this is called pooling. We are lazy so we want\n",
    "% to define three things in one go!\n",
    "peg(a;b;c).\n",
    "\n",
    "% Let's do the same for disks. We will have four, with ids 1,2,3 and 4.\n",
    "% This is also a shortcut notation called an interval.\n",
    "disk(1..4)."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "seventh-slovakia",
   "metadata": {},
   "source": [
    "When the program runs above, we see our intervals and pools flushed out into the full lits of objects. We now have four disks, named by numbers, and three pegs, a b and c. Great!\n",
    "\n",
    "## 2. Create States\n",
    "\n",
    "The next set of atoms we want to create is related to states of the game. How does it start? How should it end? We thus want to define:\n",
    "\n",
    "1. an init state\n",
    "2. a \"yay we're done!\" state\n",
    "\n",
    "Knowing that we have pegs and disks, it follows that the states should include how we expect the pegs and disks to be. For example, we know that we will start with all four disks on peg a. We know that we must finish with all four disks on beg c. So how about we make a rule for each of init and finish that represents a state, along with some number (the ids) of disks on some peg. That might look like this:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 27,
   "id": "champion-wholesale",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "clingo version 5.4.0\n",
      "Reading from stdin\n",
      "Solving...\n",
      "Answer: 1\n",
      "init_on(1,a)\n",
      "init_on(2,a)\n",
      "init_on(3,a)\n",
      "init_on(4,a)\n",
      "finish_on(1,c)\n",
      "finish_on(2,c)\n",
      "finish_on(3,c)\n",
      "finish_on(4,c)\n",
      "SATISFIABLE\n",
      "\n",
      "Models       : 1\n",
      "Calls        : 1\n",
      "Time         : 0.000s (Solving: 0.00s 1st Model: 0.00s Unsat: 0.00s)\n",
      "CPU Time     : 0.000s\n"
     ]
    }
   ],
   "source": [
    "%%clingo --out-ifs=\\\\n 0\n",
    "\n",
    "% start the game with disks 1, 2, 3, 4 (\"one through four\") on peg a\n",
    "init_on(1..4,a).\n",
    "\n",
    "% finish the game with those same disks one through four on peg c.\n",
    "finish_on(1..4,c)."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "computational-avatar",
   "metadata": {},
   "source": [
    "We can again see when we run the code that our `init_on` and `finish_on` states are actually a bunch of different facts that must all be true to determine that we've started or finished. Okay, great! Just to show you, let's say I was writing my program and I didn't want any facts to print (because I'm not done yet!). I can add `#show.`."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 28,
   "id": "authentic-brown",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "clingo version 5.4.0\n",
      "Reading from stdin\n",
      "Solving...\n",
      "Answer: 1\n",
      "\n",
      "SATISFIABLE\n",
      "\n",
      "Models       : 1\n",
      "Calls        : 1\n",
      "Time         : 0.000s (Solving: 0.00s 1st Model: 0.00s Unsat: 0.00s)\n",
      "CPU Time     : 0.000s\n"
     ]
    }
   ],
   "source": [
    "%%clingo --out-ifs=\\\\n 0\n",
    "\n",
    "% start the game with disks 1, 2, 3, 4 (\"one through four\") on peg a\n",
    "init_on(1..4,a).\n",
    "\n",
    "% finish the game with those same disks one through four on peg c.\n",
    "finish_on(1..4,c).\n",
    "#show."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "opposite-falls",
   "metadata": {},
   "source": [
    "The command was previously called `#hide.` but it [doesn't seem to exist anymore](https://sourceforge.net/p/potassco/mailman/message/34222978/). Hiding everything is essentially asking it to show nothing in particular, as we did above. Next, let's define some boundaries.\n",
    "\n",
    "## 3. Create Boundaries\n",
    "\n",
    "A boundary is a limit, or in the case of the Towers of Hanoi, the maximum number of moves we want to allow. For a solver, we can use this as a strategy to prevent it from finding a infinitely large number of solutions with infinite moves (yeah, yikes!). So let's say that we limit to just 15 moves. That is actually really easy to state, like this: \n",
    "\n",
    "```\n",
    "% We are only allowed 15 moves.\n",
    "moves(15).\n",
    "```\n",
    "\n",
    "## 4. Define Rules\n",
    "\n",
    "Now it get's fun! Let's pretend that you are staring at three pegs, and the four disks are on the left one. How do you know if you can move one disk to another peg? As a reminder of the problem:\n",
    "\n",
    "1. You can only move a peg on the top of a stack\n",
    "2. You can only put a peg on top of another one that is larger.\n",
    "\n",
    "This means that we need a few rules. We need to define what constitutes a disk being blocked, meaning that we cannot reach it. We also need to define what a valid move looks like, namely moving a disk on the top of a stack to another peg.\n",
    "\n",
    "### Generate Moves\n",
    "\n",
    "Let's take a mindset that we are going to generate a whole ton of valid moves, and just have them available for other rules to act on. This is probably one of the harder lines to write, so let's walk through it. First, here is the whole thing:\n",
    "\n",
    "```\n",
    "1 { move(D,P,T) : disk(D), peg(P) } 1 :- moves(M), T = 1..M.\n",
    "```\n",
    "Let's first notice that the \"if\" that we recognize (the `:-` is right before `moves(M)`, meaning that the head of the rule is `1 { move(D,P,T) : disk(D), peg(P) } 1`. Whenever you see numbers surrounding some statement in curly brackets, this is a cardinality constaint. Whatever we are stating in those brackets, there can only be one. If you defined other numbers, it would increase the allowed range.  Now let's look at the set of statements inside the curly brackets, namely `move(D,P,T) : disk(D), peg(P)`. This is a set that is expanded using the predicates behind the colons. I was reading it as:\n",
    "\n",
    "```\n",
    "% Move D, P, and T, given that D is a disk, and P is a peg.\n",
    "move(D,P,T) : disk(D), peg(P).\n",
    "```\n",
    "\n",
    "Okay now let's add the rule body, everything after the `:-`.\n",
    "\n",
    "```\n",
    "% Move D, P, and T, given that D is a disk, and P is a peg IF we have M moves, and \n",
    "% some timepoint (T) is between 1 and M.\n",
    "1 { move(D,P,T) : disk(D), peg(P) } 1 :- moves(M), T = 1..M.\n",
    "```\n",
    "\n",
    "If you had to write this, you'd probably start with the first statement in the head `move(D, P, T)` and then realize that you need to ensure that D is a disk and P is a peg (the second part) and a move is only one of these statements (add the curly brackets and cardinality of 1) and then you would think through the IF. \"Move disk D, peg P, for some timepoint T IF we have 15 moves, AND (the command) the timepoint can go from 1 through M.\n",
    "You might not have included the variable T when you first wrote it, but you'd eventually realize you need to scope one move to one timepoint. If you consider that we will are trying to figure out values for pegs and disks, this rule is actually setting the timepoint, T, to be each number between 1 and M and then solving for the peg and disk.\n",
    "\n",
    "But we can't just give that to the program, because we haven't provided any constraints. This rule would basically let us move any disk to any peg at any time. We haven't told it how to move, or what it means to be blocked. If we try running it, it will run forever (at least as far as I can tell). I interrupted the kernel process below because it seemed to run forever:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "hearing-hearing",
   "metadata": {},
   "outputs": [],
   "source": [
    "%%clingo --out-ifs=\\\\n 0\n",
    "\n",
    "% Create three pegs, (a, b and c) and four disks\n",
    "peg(a;b;c).\n",
    "disk(1..4).\n",
    "\n",
    "% A maximum of 15 moves is allowed\n",
    "moves(15).\n",
    "\n",
    "1 { move(D,P,T) : disk(D), peg(P) } 1 :- moves(M), T = 1..M."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "plain-buffer",
   "metadata": {},
   "source": [
    "## Define Valid Moves\n",
    "\n",
    "Conceptually, we now have a bunch of options for moves, `move(D,P,T)` to say we are moving a disk relative to a peg for a timepoint, T. The next part is tricky, because we have to define what it means to be at a timepoint. Let's call this `at_timepoint(D, P, T)`. First we might say that, \"We are at timepoint zero (the beginning of the game) IF we are in state init on\":\n",
    "\n",
    "```\n",
    "% We are at timepoint 0 IF we are in state init on.\n",
    "% If you recall from above, init_on also takes a disk and peg.\n",
    "at_timepoint(D, P, 0) :- init_on(D,P).\n",
    "```\n",
    "And we are at a general timepoint for any given value of T if we move there:\n",
    "\n",
    "```\n",
    "% moving a disk changes its location\n",
    "at_timepoint(D, P, T) :- move(D, P, T).\n",
    "```\n",
    "\n",
    "The next rule you'll try to write ensures that a timepoint, T+1, comes after a timepoint T. It also ensures that we haven't reached the maximum number of allowed moves (since we start counting at 0, we go up to T but not equal to it). You might first try this:\n",
    "\n",
    "```\n",
    "% We can be at the next timepoint, T+1, if we're at timepont T, and we haven't taken a move for this \n",
    "% timepoint yet, and we're not at the maximum number of moves.\n",
    "at_timepoint(D,P,T+1) :- at_timepoint(D,P,T), not move(D,P,T+1), not moves(T).\n",
    "```\n",
    "\n",
    "But there's a problem, and that's the rule `move(D, P, T+1)`. You see, we don't actually care about what peg was relevant for that move. We only care if a disk was moved at that timepoint, to any peg. To fix this rule, we need to write another one that elimiates the variable \"D\", because we don't care about the disk. This says that \"We move a disk at a timepoint given any move of a disk D, at timepoint T for ANY peg (why we replace the variable P with an underscore, `_`.\n",
    "\n",
    "```\n",
    "% We move any disk at a timepoint IF we've moved a disk at a time point for any peg, we don't care -> (_)\n",
    "move(D,T)   :- move(D,_,T).\n",
    "```\n",
    "\n",
    "Now we can write this rule more correctly:\n",
    "\n",
    "```\n",
    "% We can be at the next timepoint, T+1, if we're at timepont T, and we haven't taken a move for this \n",
    "% timepoint yet, and we're not at the maximum number of moves.\n",
    "at_timepoint(D,P,T+1) :- at_timepoint(D,P,T), not move(D,T+1), not moves(T).\n",
    "```\n",
    "\n",
    "## Define Blocked\n",
    "\n",
    "We're close! So far we've defined what it means to move any disk to any peg at a timepoint, and we also now have temporal constraints that T+1 must come after T. What is missing? The idea of a blocked disk! Right now, there is nothing stopping you from grabbing a disk that is second on a peg. For these next statements, it's also important to remember that disks are numbered based on size, with smaller numbers indicating smaller disks.\n",
    "\n",
    "```\n",
    "% A smaller disk (D-1) for some peg P, and the next timepoint, T+1, is blocked IF:\n",
    "% the current timepoint has a larger disk (D) on the same peg, and we haven't\n",
    "% finished making all of our moves.\n",
    "\n",
    "% In simpler terms, the disk below a position is blocked\n",
    "blocked(D-1,P,T+1) :- at_timepoint(D,P,T), disk(D), not moves(T).\n",
    "```\n",
    "\n",
    "Basically, you can't move a disk if there is a larger one on top of it from one timepoint to the next. But what about the same timepoint? Similarity, a smaller disk is blocked for some timepoint T if it's blocked. This sounds weird to read, but we are basically using the same rule to not be rudundant, but changing the value of T.\n",
    "\n",
    "```\n",
    "% The disk below a blocked position is also blocked\n",
    "blocked(D-1,P,T)   :- blocked(D,P,T), disk(D).\n",
    "```\n",
    "\n",
    "Both the rules together say that a smaller disk is blocked for the current or next timepoint if our current timepoint has a larger disk on the same peg. I'm not sure if `at_timepoint` is the best descriptor, I think in the first examples they had the rule written as `on(D,P,T)` which more directly says \"We are ON this current move.\"\n",
    "\n",
    "## Integrity Constraints\n",
    "\n",
    "Rules that start with `:-` are integiry constraints. They fail whenever all the literals (in the body) are true. This means that these statements cannot be true.\n",
    "\n",
    "```\n",
    "% A blocked disk (smaller in size on the same peg) cannot be moved\n",
    ":- move(D,P,T), blocked(D-1,P,T).\n",
    "\n",
    "% A disk can only be placed on top of a bigger disk. \n",
    ":- move(D,T), at_timepoint(D,P,T-1), blocked(D,P,T).\n",
    "\n",
    "% We finish when we reach moves M, not at that timepoint (because we start countint at 0)\n",
    ":- finish_on(D,P), not at_timepoint(D,P,M), moves(M).\n",
    "\n",
    "% Re-iteration of the first rule to speed up the problem\n",
    ":- not 1 { at_timepoint(D,P,T) : peg(P) } 1, disk(D), moves(M), T = 1..M.\n",
    "```\n",
    "\n",
    "# All Together Now!\n",
    "\n",
    "Let's put the pieces together and run the full program. We will add a show statement at the end to show moves."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "outdoor-springer",
   "metadata": {},
   "outputs": [],
   "source": [
    "%%clingo --out-ifs=\\\\n 0\n",
    "\n",
    "% Create three pegs, (a, b and c) and four disks\n",
    "peg(a;b;c).\n",
    "disk(1..4).\n",
    "\n",
    "% start the game with disks 1, 2, 3, 4 (\"one through four\") on peg a\n",
    "init_on(1..4,a).\n",
    "\n",
    "% finish the game with those same disks one through four on peg c.\n",
    "finish_on(1..4,c).\n",
    "\n",
    "\n",
    "% A maximum of 15 moves is allowed\n",
    "moves(15).\n",
    "\n",
    "1 { move(D,P,T) : disk(D), peg(P) } 1 :- moves(M), T = 1..M.\n",
    "\n",
    "% We are at timepoint 0 IF we are in state init on.\n",
    "% If you recall from above, init_on also takes a disk and peg.\n",
    "at_timepoint(D, P, 0) :- init_on(D,P).\n",
    "\n",
    "% moving a disk changes its location\n",
    "at_timepoint(D, P, T) :- move(D, P, T).\n",
    "\n",
    "move(D,T)   :- move(D,_,T).\n",
    "\n",
    "% We can be at the next timepoint, T+1, if we're at timepont T, and we haven't taken a move for this \n",
    "% timepoint yet, and we're not at the maximum number of moves.\n",
    "at_timepoint(D,P,T+1) :- at_timepoint(D,P,T), not move(D,T+1), not moves(T).\n",
    "\n",
    "% In simpler terms, the disk below a position is blocked\n",
    "blocked(D-1,P,T+1) :- at_timepoint(D,P,T), disk(D), not moves(T).\n",
    "\n",
    "% The disk below a blocked position is also blocked\n",
    "blocked(D-1,P,T)   :- blocked(D,P,T), disk(D).\n",
    "\n",
    "\n",
    "\n",
    "% A blocked disk (smaller in size on the same peg) cannot be moved\n",
    ":- move(D,P,T), blocked(D-1,P,T).\n",
    "\n",
    "% A disk can only be placed on top of a bigger disk. \n",
    ":- move(D,T), at_timepoint(D,P,T-1), blocked(D,P,T).\n",
    "\n",
    "% We finish when we reach moves M, not at that timepoint (because we start countint at 0)\n",
    ":- finish_on(D,P), not at_timepoint(D,P,M), moves(M).\n",
    "\n",
    "% Re-iteration of the first rule to speed up the problem\n",
    ":- not 1 { at_timepoint(D,P,T) : peg(P) } 1, disk(D), moves(M), T = 1..M.\n",
    "\n",
    "#show move/3."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "dynamic-motion",
   "metadata": {},
   "source": [
    "And there you have it! We've come up with a solution for moving disks. Could you think of a cool way to visualize this output? If you'd like to improve this tutorial, please [visit the repository](https://github.com/vsoch/clingo-lessons) and open an issue or a pull request. I wrote it very late so who knows where my brain is. :)"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.8.8"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
